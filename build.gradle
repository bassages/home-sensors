plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.github.ben-manes.versions' version '0.52.0'
    id 'jacoco'
    id 'org.sonarqube' version '6.2.0.5505'
}

if (System.getenv('GITHUB_TAG') != null) {
    version = System.getenv("GITHUB_TAG")
    logger.info('Creating release from GitHub action workflow. Version: ' + version)
} else {
    version = 'x.x.x'
}

jar {
    //noinspection GroovyAssignabilityCheck
    archiveBaseName = 'home-sensors'
}

jacoco {
    toolVersion = '0.8.11'
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
        html.required = false
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
    }
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.junit.reportsPath", "build/test-results"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.organization", "bassages-github"
        property "sonar.projectKey", "home-sensors"
    }
}

java {
    toolchain{
        languageVersion = JavaLanguageVersion.of(21)
    }
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Werror"
}

compileTestJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Werror"
}

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor('org.projectlombok:lombok:1.18.38')
    compileOnly('org.projectlombok:lombok:1.18.38')

    implementation('commons-io:commons-io:2.19.0')
    implementation('org.apache.commons:commons-lang3:3.17.0')
    implementation('net.sf.expectit:expectit-core:0.9.0')
    implementation('com.jcraft:jsch:0.1.55')
    implementation('javax.annotation:javax.annotation-api:1.3.2')
    implementation('javax.inject:javax.inject:1')

    // for next dependencies, the version are managed by Spring Boot,
    // see https://docs.spring.io/spring-boot/docs/current/reference/html/appendix-dependency-versions.html
    compileOnly("org.springframework.boot:spring-boot-devtools")

    implementation('org.springframework.boot:spring-boot-starter')
    implementation('org.springframework.boot:spring-boot-starter-logging')

    implementation('com.fasterxml.jackson.core:jackson-core')
    implementation('com.fasterxml.jackson.core:jackson-databind')
    implementation('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')

    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation('nl.jqno.equalsverifier:equalsverifier:4.0.3')
    testImplementation('org.wiremock:wiremock-standalone:3.13.1')
}
